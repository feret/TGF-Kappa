/* Rules */

/* TGFB1, MMP2 and MMP14 have two forms ; there are synthezyzed as latent molecule (state~latent) and activated toward an active form (state~active). Warning ; LAP-TGFB1 is the latent form of TGFB1 and we keep this term in the text in order to facilitate the undersatnding.*/
/*HYpothesis: rule of association is counterbalanced by rule of dissociation ie ; association (binary rule) A + B -> AB @ k where rate is k[A][B] ; dissociation (unary rule) AB -> A + B @ k where rate is k[AB]. Rate for dissociation is commonly choosen higher than rate for association to compensate the lack of factor in the product. However we can use two rates for dissociation : a lower one for stable structure such as ternary complex and a higher dissociation rate for lesser stable structures*/

/*LAP_TGFB1 binds to LTBP(TB)[15611103]. The 8-Cys3 domain of LTBP1 binds the Small Latent Complex, SLC of TGFbeta [8617200,8939931]*/
'TGFB1(state~latent)_LTBP1' TGFB1(state{latent},w[.]), LTBP1(TB[.]) ->        TGFB1(state{latent},w[1]), LTBP1(TB[1]) | 1 r1 @ 'high_affinity_bin'
'TGFB1(state~latent)_LTBP1dissociate' TGFB1(state{latent},w[1]), LTBP1(TB[1]) -> TGFB1(state{latent},w[.]), LTBP1(TB[.]) | 1 r2 @ 'low_affinity_un'

/* LTBP1 binds to FN1. FROM [20699357], Interactions have been reported between fibronectin and the N-terminal region of LTBP-1 by ELISA and blot overlay assays [16260650]. However, using solid-phase binding assays, no interactions of full-length LTBP1 or LTBP1 fragments with fibronectin were detected, except in the presence of heparin, which binds a hinge region of LTBP1 [17580303].*/
'LTBP1_FN1' LTBP1(TB[_],Nter[.]), FN1(y[.]) -> LTBP1(TB[_],Nter[1]), FN1(y[1]) | 1 r3 @ 'low_affinity_bin'
'LTBP1_HS' LTBP1(TB[_],hinge[.]), HS(a[.]) -> LTBP1(TB[_],hinge[1]), HS(a[1]) | 1 r4 @ 'normal_affinity_bin'
'LTBP1_HS_FN1' LTBP1(TB[_],hinge[1], Nter[.]), HS(a[1]), FN1(y[.]) -> LTBP1(TB[_],hinge[1], Nter[2]), HS(a[1]), FN1(y[2]) | 1 r5 @ 'high_affinity_bin'
'LTBP1_FN1_dissociate' LTBP1(TB[_],Nter[1]), FN1(y[1]) -> LTBP1(TB[_],Nter[.]), FN1(y[.]) | 1 r6 @ 'normal_affinity_un'
'LTBP1_HS_dissociate' LTBP1(TB[_],hinge[1]), HS(a[1]) -> LTBP1(TB[_],hinge[.]), HS(a[.]) | 1 r7 @ 'high_affinity_un'
'LTBP1_HS_FN1dissociate' LTBP1(TB[_],hinge[1], Nter[2]), HS(a[1]), FN1(y[2]) -> LTBP1(TB[_],hinge[1], Nter[.]), HS(a[1]), FN1(y[.]) | 1 r8 @ 'low_affinity_un'

/* LTBP1 binds to fibrillin1. FROM [12429738] the binding site was within three domains of the LTBP1 C terminus, and in fibrillin1 the site was defined within four domains near the N terminus .FROM [20699357]the deposition of LTBP-1-containing LLC is governed by pericellular assembly of fibrillin microfibrils through interactions with heparan sulfate and with microfibril-associated molecules that fine-tune TGFβ sequestration Thus, the cell-surface heparan-sulfate-rich microenvironment is an essential orchestrator of TGFβ sequestration within microfibrils, which also interact with fibronectin during higher-order fibrillogenesis. Full-length LTBP1 bound only weakly to N-terminal pro-fibrillin1, but this association was strongly enhanced by heparin. C-terminal LTBP-1 binds well to the first hybrid domain and subsequent cbEGF domain. The EGF preceding the first hybrid domain influences this interaction, and the PF1Ex5-7 subfragment binds more strongly than does PF1 itself. These data indicate that the fibrillin-1-binding region for LTBP1 must be conformation-dependent.*/
'LTBP1_FBN1' LTBP1(TB[_],Cter[.]), FBN1(Hyb1[.]) -> LTBP1(TB[_],Cter[1]), FBN1(Hyb1[1]) | 1 r9 @ 'low_affinity_bin'
/*'LTBP1_HS' LTBP1(TB[_],hinge[.]), HS(a[.]) -> LTBP1(TB[_],hinge[1]), HS(a[1]) @ 'normal_affinity_bin' /*_Already written above*/*/
'LTBP1_HS_FBN1' LTBP1(TB[_],hinge[1], Cter[.]), HS(a[1]), FBN1(Hyb1[.]) -> LTBP1(TB[_],hinge[1], Cter[2]), HS(a[1]), FBN1(Hyb1[2]) | 1 r10 @ 'high_affinity_bin'
'LTBP1_FBN1_dissociate' LTBP1(TB[_],Cter[1]), FBN1(Hyb1[1]) -> LTBP1(TB[_],Cter[.]), FBN1(Hyb1[.]) | 1 r11 @ 'normal_affinity_un'
/*'LTBP1_HSdissociate' LTBP1(TB[_],hinge[1]), HS(a[1]) -> LTBP1(TB[_],hinge[.]), HS(a[.]) @ 'normal_affinity_un' /*_Already written above*/*/
'LTBP1_HS_FBN1_dissociate' LTBP1(TB[_],hinge[1], Cter[2]), HS(a[1]), FBN1(Hyb1[2]) -> LTBP1(TB[_],hinge[1], Cter[.]), HS(a[1]), FBN1(Hyb1[.]) | 1 r12 @ 'low_affinity_un'

/* MAGP1(Cter) alias MFAP2 bound to a region at the Nterminus of fibrillin1 [15039439,11481325,18669635] and inhibits LTBP1 binding to fibrillin1 , the binding of MAGP1 and LTBP1 to fibrillin1 might be mutually exclusive [20699357](Hyb1 must be free to permit MAGP1 binding to FBN1). Note that MAGP1 did not bind immobilized fulllength LTBP1 [20699357]*/
'MAGP1_FBN1' MAGP1(Cter[.]), FBN1(Nter[.],Hyb1[.]) -> MAGP1(Cter[1]), FBN1(Nter[1],Hyb1[.]) | 1 r13 @ 'normal_affinity_bin'
'MAGP1_FBN1_dissociate' MAGP1(Cter[1]), FBN1(Nter[1],Hyb1[.]) -> MAGP1(Cter[.]), FBN1(Nter[.],Hyb1[.]) | 1 r14 @ 'high_affinity_un'

/* Binding FN1 to ITGA5B1 and fibril formation. FROM [7691819,]alpha 5 beta 1 integrins, the typical fibronectin receptor, not only mediate cell adhesion to fibronectin, but also play an essential role in the assembly of a fibronectin matrix. FROM [8521488]First, multiple Fn-binding integrins can support matrix assembly; their activation state controls fibrillogenesis. Second, Fn binding to cells expressing an activated integrin is necessary but not sufficient for matrix assembly. Additional “postoccupancy” events involving the integrin β, but not the α subunit, cytoplasmic domain are needed. Third, these postoccupancy events require an intact actin cytoskeleton. FROM [10704455]whereas the vitronectin receptor alpha(v)beta(3) remains within focal contacts, the fibronectin receptor alpha(5)beta(1) translocates from focal contacts into and along extracellular matrix (ECM) contacts. Translocating alpha(5)beta(1)integrins induce initial fibronectin fibrillogenesis by transmitting cytoskeleton-generated tension to extracellular fibronectin molecules. FROM[12857786],FN matrix assembly is a cell-mediated process in which soluble dimeric FN is converted into a fibrillar network. Binding of cell surface integrin receptors to FN converts it to an active form, which promotes fibril formation through interactions with other cell-associated FN dimers. FROM [16567085]the 70-kDa region of fibronectin binds to linearly arrayed cell surface molecules of adherent cells to initiate assembly, display of the arrays is controlled by the integrin that mediates adhesion, and fibronectin-binding integrins promote fibronectin-fibronectin interactions during progression of assembly. FROM [16984808]Fibronectin orchestrates the assembly of ECM. From [18653538]Assembly is initiated by Nterminal fibronectin interactions with the cell surface, which induces α5β1 integrin clustering and translocation from focal contacts into fibrillar adhesions, followed by fibronectin self-association. In addition to α5β1integrin binding to fibronectin's RGD-containing domain, cells must bind with high affinity to fibronectin's amino-terminal 29-kDa matrix assembly domain to form a fibronectin matrix. FROM[7961716, 9446562, 8034677, 8609174]Interaction with integrins is hypothesized to promote extension of FN and thereby facilitate interactions of the N-terminal 70K region with other parts of FN, notably the first type III repeat (III1). Fibronectin matrix assembly is thought to involve binding interactions between N-ter and III1*/
/* For the dissociation rules of FN1_ITGA5B1_FN1, we include three dissociation rules for ITGA5B1(RGD) and FN1 and three dissociation rules for FN1_FN1 depending on the size of the polymers.*/
'FN1_ITGA5B1' FN1(RGD[.]), ITGA5B1(x[.]) -> FN1(RGD[1]), ITGA5B1(x[1]) | 1 r15 @ 'high_affinity_bin'
'FN1(RGD)_FN1' FN1(RGD[_], Nter[.]), FN1(III1[.],Nter[.]) -> FN1(RGD[_], Nter[1]), FN1(III1[1], Nter[.]) | 1 r16 @ 'high_affinity_bin'
'FN1(Nter)_FN1(III1)_FN1' FN1(RGD[_],Nter[1]), FN1(III1[1], Nter[.]), FN1(III1[.],Nter[.]) -> FN1(RGD[_],Nter[1]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.]) | 1 r17 @ 'high_affinity_bin'

'FN1_ITGA5B1_dissociate1' FN1(RGD[1],Nter[.]), ITGA5B1(x[1]) -> FN1(RGD[.],Nter[.]), ITGA5B1(x[.]) | 1 r18 @ 'very_high_affinity_un'
'FN1_ITGA5B1_dissociate2' FN1(RGD[1],Nter[2]), FN1(III1[2],Nter[.]), ITGA5B1(x[1]) -> FN1(RGD[.],Nter[2]), FN1(III1[2],Nter[.]), ITGA5B1(x[.]) | 1 r19 @ 'high_affinity_un'
'FN1_ITGA5B1_dissociate3' FN1(RGD[1],Nter[2]), FN1(III1[2],Nter[3]), FN1(III1[3]), ITGA5B1(x[1]) -> FN1(RGD[.],Nter[2]), FN1(III1[2],Nter[3]), FN1(III1[3]), ITGA5B1(x[.]) | 1 r20 @ 'low_affinity_un'
'FN1(Nter)_FN1(III1)_FN1_dissociate1' FN1(Nter[1]), FN1(III1[1], Nter[2]), FN1(III1[2]) -> FN1(Nter[1]), FN1(III1[1],Nter[.]), FN1(III1[.]) | 1 r21 @ 'low_affinity_un'
'FN1(Nter)_FN1(III1)_FN1_dissociate2' FN1(RGD[_],Nter[1]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.]) -> FN1(RGD[_],Nter[.]), FN1(III1[.], Nter[2]), FN1(III1[2],Nter[.]) | 1 r22 @ 'low_affinity_un'
'FN1(Nter)_FN1(III1)_FN1_dissociate3' FN1(RGD[_],Nter[1]), FN1(III1[1],Nter[.]) -> FN1(RGD[_],Nter[.]), FN1(III1[.],Nter[.]) | 1 r23 @ 'high_affinity_un'

/* FBN1 binds HS leading to FBN1s assembly. FROM [11461921], by using different binding assays, we localize two calcium-independent heparin-binding sites to the N-terminal (Arg(45)-Thr(450)) and C-terminal (Asp(1528)-Arg(2731)) domains of FBN1. Interaction of FBN1 with heparan sulfate containing proteoglycans are necessary to mediate or nucleate FBN1 assembly. NOTE: FROM [16492675] Heparan sulfate negatively modulates TGFB1 responsiveness by decreasing the ratio of TGFB1 binding to TbetaR-II and TbetaR-I, facilitating caveolae/lipid-raft-mediated endocytosis and rapid degradation of TGfB1, thus diminishing non-lipid-raft-mediated endocytosis and signaling of TGFB1 in these epithelial cells (CHO).*/
'HS_FBN1(HSB)' HS(x[.]), FBN1(HSBS[.]) -> HS(x[1]), FBN1(HSBS[1]) | 1 r24 @ 'normal_affinity_bin'
'HS_FBN1(HSB)_dissociate' HS(x[1]), FBN1(HSBS[1]) -> HS(x[.]), FBN1(HSBS[.]) | 1 r25 @ 'high_affinity_un'

/* Protein-binding assays demonstrated interactions of fibronectin with a Cterminal region of fibrillin1, fibrillin2, and fibrillin3 (and with an Nterminal region of fibrillin1). The Cterminal of fibrillins interacted strongly with fibronectin as multimers, but not as monomers. Mapping studies revealed that the major binding interaction between fibrillins and fibronectin involves the collagen-gelatin-binding (CBD)region between domains FNI(6) and FNI(9)[19037100]. Note that the collagen region of FN1 is include in Nter domain.*/
'FN1_FBN1(Cter)' FN1(RGD[_],Nter[1]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.],CBD[.]), FBN1(HSBS[_],Cter[.]) -> FN1(RGD[_],Nter[1]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.],CBD[3]), FBN1(HSBS[_],Cter[3]) | 1 r26 @ 'high_affinity_bin'
'FN1_FBN1(Cter)_dissociate' FN1(RGD[_],Nter[1]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.],CBD[3]), FBN1(HSBS[_],Cter[3]) -> FN1(RGD[_],Nter[1]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.],CBD[.]), FBN1(HSBS[_],Cter[.]) | 1 r27 @ 'low_affinity_un'

/* From [20699357], LLC or LAP_TGFB1_LTBP1 sequestration in the matrix is a poorly understood process. The contextual environment that facilitates this sequestration is proposed to depend on LTBP1 association with fibronectin fibrils and their relocation to fibrillin microfibrils [19525102]. Deposition of a large latent TGFbeta complex (LLC), which contains latent TGFbeta-binding protein 1 (LTBP1), is directly dependent on the pericellular assembly of fibrillin microfibrils, which interact with fibronectin during higher-order fibrillogenesis. NOTE: the complication at this step is that both FN1 and FBN1 bind to LTBP1, FN1 and FBN1 form fibrills, FBN1 binds to FN1 multimers and all these processes are facilitated by HS and leading to TGFB1 storing. In our model, we postulate that FN1 and FBN1 are associated for storing LLC which can bind either FN1 (y site) or FBN1 (Hyb site).*/
'FN1fibril_FBN1_HSfibril_LTBP1_TGFB1' FN1(RGD[_],Nter[1],y[.]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.],CBD[3]), FBN1(HSBS[_],Cter[3],Hyb1[.]), LTBP1(TB[_],hinge[_],Nter[.],Cter[.]) -> FN1(RGD[_],Nter[1],y[4]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.],CBD[3]), FBN1(HSBS[_],Cter[3],Hyb1[5]), LTBP1(TB[_],hinge[_],Nter[4],Cter[5]) | 1 r28 @ 'very_high_affinity_bin'
'FN1fibril_FBN1_HSfibril_LTBP1_TGFB1_dissociate' FN1(RGD[_],Nter[1],y[4]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.],CBD[3]), FBN1(HSBS[_],Cter[3],Hyb1[5]), LTBP1(TB[_],hinge[_],Nter[4],Cter[5]) -> FN1(RGD[_],Nter[1],y[.]), FN1(III1[1], Nter[2]), FN1(III1[2],Nter[.],CBD[3]), FBN1(HSBS[_],Cter[3],Hyb1[.]), LTBP1(TB[_],hinge[_],Nter[.],Cter[.]) | 1 r29 @ 'high_affinity_un'

/* Binding FN1 and ITGAVB3. From [18653538] although integrin α5β1 is a primary regulator of extracellular fibronectin assembly (16061370), αvβ3 integrin also supports the assembly of short pericellular fibronectin fibrils (17591922). FROM (17591922)The GNGRG motif in FN-I5 is a novel αvβ3 binding site that can function for FN matrix assembly*/
'FN1_ITGAVB3' FN1(NGR[.]), ITGAVB3(y[.]) -> FN1(NGR[1]), ITGAVB3(y[1]) | 1 r30 @ 'normal_affinity_bin'
'FN1(NGR)_FN1' FN1(NGR[_], Nter[.]), FN1(III1[.],Nter[.]) -> FN1(NGR[_], Nter[1]), FN1(III1[1], Nter[.]) | 1 r31 @ 'normal_affinity_bin'
'FN1_ITGAVB3_dissociate' FN1(NGR[1]), ITGAVB3(y[1]) -> FN1(NGR[.]), ITGAVB3(y[.]) | 1 r32 @ 'high_affinity_un'
'FN1(NGR)_FN1_dissociate' FN1(NGR[_], Nter[1]), FN1(III1[1], Nter[.]) -> FN1(NGR[_], Nter[.]), FN1(III1[.],Nter[.]) | 1 r33 @ 'high_affinity_un'

/* Binding of Fibulin with FBN1. FROM [8702639, 15790312], Fibulin-2 and -5 were shown to interact with fibrillin-1, and the binding epitopes were mapped to a relatively large N-terminal region of fibrillin-1. FROM [17255108], the first hybrid domain in fibrillin-1 is essential for binding to fibulin-2, -4, and -5. FROM [19349279], Fibulin 4 (but also fibulin2 and 5) binds to residues contained within EGF3/Hyb1 of fibrillin1 and inhibit the interaction of C-terminal LTBP1 with fibrillin1. Interactions between LTBP-1 and fibrillin-1 compete for interactions between fibrillin-1 and these fibulin. BUT FROM [20699357], Massam-Wu et al repport that FBLN4 can strongly bind LTBP1, and can simultaneously bind LTBP1 and fibrillin1. FBLN4 could form a stable ternary complex with full-length LTBP1 and fibrillin1. They conclude that FBN1 binding region for LTBP1 must be conformation-dependent. NOTE: In the model, we choose the competition between FBLN4 and LTBP1 for the Hyb site from FBN1.*/
'FBN1_FBLN4' FBN1(Hyb1[.]), FBLN4(y[.]) -> FBN1(Hyb1[1]), FBLN4(y[1]) | 1 r34 @ 'normal_affinity_bin'
'FBN1_FBLN4_dissociate' FBN1(Hyb1[1]), FBLN4(y[1]) -> FBN1(Hyb1[.]), FBLN4(y[.]) | 1 r35 @ 'high_affinity_un'

/* Fibulin 1 binds to FN1. FROM [1400330], Fibulin binds to itself and to the carboxyl-terminal heparin-binding region of fibronectin.The binding of fibulin to solid-phase FN was estimated to have a Kd of 139 nM, whereas the Kd for self-interaction was 322 nM. Evaluation of proteolytic fragments from all regions of FN allowed a fibulin-binding site to be localized within a 23-kDa heparin-binding fragment containing type III repeats 13-14. Heparin did not compete for the interaction between fibulin and FN, suggesting that the binding sites for fibulin and heparin are distinct.FROM [8354280] fibulin-1 has been shown to bind the extracellular matrix proteins fibronectin (FN), 1 nidogen, and laminin. NOTE: Cter site from FN1 is involved in binding with FBLN1 and ADAMTS4 (spacer site).*/
'FN1_FBLN1' FN1(Cter[.]), FBLN1(z[.]) -> FN1(Cter[1]), FBLN1(z[1]) | 1 r36 @ 'normal_affinity_bin'
'FN1_FBLN1_dissociate' FN1(Cter[1]), FBLN1(z[1]) -> FN1(Cter[.]), FBLN1(z[.]) | 1 r37 @ 'high_affinity_un'

/* Integrins bind to latentTGFB1. FROM [22025551] six (αvβ1,αvβ3, αvβ5, αvβ6 and αvβ8) of the 24 currently described integrins recognize and bind arginine-glycine-aspartate (RGD) motifs in the latency-associated peptide of both TGF-β1 and TGF-β3. Four of these have been reported to activate TGF-β in vitro, including αvβ6 [10025398], αvβ8 [11970960], αvβ3 [16301681], and αvβ5 [16436664,18086923]. Integrin-mediated TGF-β activation has been best characterized for the αvβ6 and the αvβ8 integrins. Activation of TGF-β by the αvβ8 integrin involves MMP14 and proteolytic cleavage of the latent TGF-β molecule [18243766], whereas αvβ3, αvβ5, and αvβ6 integrins activate TGF-β by a mechanism requiring an intact cytoskeleton and cell contraction[10025398,18086923,16710477,19147812]. Activation of TGF-β by αvβ6 integrins is spatially restricted to epithelial cells, whereas αvβ5, and to a lesser extent αvβ3, are present on mesenchymal cells and are able to activate mesenchymal TGF-β [18086923,16142753,19652365]*/
'TGFB1(state~latent)_ITGAVB3' TGFB1(state{latent},RGD[.]), ITGAVB3(x[.]) -> TGFB1(state{latent},RGD[1]), ITGAVB3(x[1]) | 1 r38 @ 'normal_affinity_bin'
'TGFB1(state~latent)_ITGAVB3_dissociate' TGFB1(state{latent},RGD[1]), ITGAVB3(x[1]) -> TGFB1(state{latent},RGD[.]), ITGAVB3(x[.]) | 1 r39 @ 'high_affinity_un'
'TGFB1(state~latent_ITGAVB5' TGFB1(state{latent},RGD[.]), ITGAVB5(x[.]) -> TGFB1(state{latent},RGD[1]), ITGAVB5(x[1]) | 1 r40 @ 'normal_affinity_bin'
'TGFB1(state~latent)_ITGAVB5_dissociate' TGFB1(state{latent},RGD[1]), ITGAVB5(x[1])-> TGFB1(state{latent},RGD[.]), ITGAVB5(x[.]) | 1 r41 @ 'high_affinity_un'
'TGFB1(state~latent)_ITGAVB6' TGFB1(state{latent},RGD[.]), ITGAVB6(x[.]) -> TGFB1(state{latent},RGD[1]), ITGAVB6(x[1]) | 1 r42 @ 'normal_affinity_bin'
'TGFB1(state~latent)_ITGAVB6_dissociate' TGFB1(state{latent},RGD[1]), ITGAVB6(x[1]) -> TGFB1(state{latent},RGD[.]), ITGAVB6(x[.]) | 1 r43 @ 'high_affinity_un'
'TGFB1(state~latent)_ITGAVB8' TGFB1(state{latent},RGD[.]), ITGAVB8(x[.]) -> TGFB1(state{latent},RGD[1]), ITGAVB8(x[1]) | 1 r44 @ 'normal_affinity_bin'
'TGFB1(state~latent)_ITGAVB8_dissociate' TGFB1(state{latent},RGD[1]), ITGAVB8(x[1]) -> TGFB1(state{latent},RGD[.]), ITGAVB8(x[.]) | 1 r45 @ 'high_affinity_un'


/* MMP14 binds to ITGAVB8-LAP_TGFB1 complex and activate TGFB1. FROM [18243766], αvβ8 is expressed in either quiescent cells or cells with a low rate of turnover and is lost in the process of neoplastic transformation. αvβ8 binds with high affinity to the RGD site of recombinant LAP-TGFB1. Upon ligation of αvβ8 with SLC, αvβ8 and MMP14 become closely associated to form a complex on the cell surface and Cell surface-associated MMP14 cleaves LAP_TGB1. FROM [11970960], αvβ8-mediated TGF-β activation is crucial for vascular and brain developmental and for suppression of adaptive immunity.*/
'TGFB1(state~latent)_ITGAVB8_MMP14' TGFB1(state{latent},RGD[1],c[.]), ITGAVB8(x[1]), MMP14(state{active},Cat[.]) -> TGFB1(state{latent},RGD[1],c[2]), ITGAVB8(x[1]), MMP14(state{active},Cat[2]) | 1 r46 @ 'high_affinity_bin'
'TGFB1(state~latent)_ITGAVB8_MMP14_dissociate' TGFB1(state{latent},RGD[1],c[2]), ITGAVB8(x[1]), MMP14(state{active},Cat[2]) -> TGFB1(state{latent},RGD[1],c[.]), ITGAVB8(x[1]), MMP14(state{active},Cat[.]) | 1 r47 @ 'normal_affinity_un'
'TGFB1(state~latent)_ITGAVB8_MMP14_Activate' ITGAVB8(x[1]), MMP14(state{active},Cat[2]), TGFB1(state{latent},RGD[1],c[2]),. -> ITGAVB8(x[.]), MMP14(state{active},Cat[.]), TGFB1(state{active},RGD[.],c[.]), iterateTGFB1() | 1 r48 @ 'very_high_affinity_un'

/* ITGAVB6 binds and activates lAP-TGFB1. FROM [10025398], LAP_TGFB1 is a ligand for the Integrin αvβ6 and ITGAVB6-Transfected Cells Induce TGF β activity. αvβ6-Mediated Activation of TGFβ1 does not require other known Activators of TGF β and Binding of LAP to αvβ6 Integrin is not sufficient for LAP_TGFB1 activation. FROM [15184403],Integrin αVβ6-mediated activation of LAP_TGFB1 requires fixed latent TGF-β. LTBP promotes αVβ6-mediated activation by both concentrating and fixing the latent complex. The LTBP-1–TGF-β complex is incorporated into the ECM and, therefore, when bound by αVβ6, promotes focal adhesion formation, force generation, and release of TGF-β from the latent complex. FROM [16260650] Fibronectin participates in latent TGF-β activation by αvβ6 integrins by binding to a region of the LTBP1 hinge sequence but FROM [17580303] no interactions of full-length LTBP1 or LTBP1 fragments with fibronectin were detected, except in the presence of heparin, which binds a hinge region of LTBP1.*/
'TGFB1(state~latent)_LTBP1_ITGAVB6' TGFB1(state{latent},w[1],RGD[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB6(x[.]) -> TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB6(x[2]) | 1 r49 @ 'normal_affinity_bin'

/* In order to have a ternary complex stable, the dissociation rule include four rules : two rules for the dissociation of LAP_TGFB1,LTBP1 which must be strong when LAP-TGFB1 is not associated with ITGAVB6 and weak when LAP-TGFB1 is associated with ITGAVB6_and two rules for the dissociation of ITGAVB6,LAP-TGFB1 which must be strong when LAP-TGFB1 is not associated with LTBP1 and weak when LAP-TGFB1 is associated with LTBP1.*/
'TGFB1(state~latent,nonITG)_LTBP1_dissociate' TGFB1(state{latent},w[1],RGD[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]) -> TGFB1(state{latent},w[.],RGD[.]), LTBP1(TB[.],hinge[_],Nter[_],Cter[_]) | 1 r50 @ 'high_affinity_un'
'TGFB1(state~latent,nonLTBP)_ITGAVB6_dissociate' TGFB1(state{latent},w[.],RGD[2]), ITGAVB6(x[2]) -> TGFB1(state{latent},w[.],RGD[.]), ITGAVB6(x[.]) | 1 r50bis @ 'high_affinity_un'
'TGFB1(state~latent,ITGAVB6)_LTBP1_dissociate' TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB6(x[2]) -> TGFB1(state{latent},w[.],RGD[2]), LTBP1(TB[.],hinge[_],Nter[_],Cter[_]), ITGAVB6(x[2]) | 1 r51 @ 'low_affinity_un'
'TGFB1(state~latent)_LTBP1_ITGAVB6_dissociate' TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB6(x[2]) -> TGFB1(state{latent},w[1],RGD[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB6(x[.]) | 1 r52 @ 'low_affinity_un'
'TGFB1(state~latent)_LTBP1_ITGAVB6_Activate' LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB6(x[2]), TGFB1(state{latent},w[1],RGD[2]),. -> LTBP1(TB[.],hinge[_],Nter[_],Cter[_]), ITGAVB6(x[.]), TGFB1(state{active},w[.],RGD[.]), iterateTGFB1() | 1 r53 @ 'high_affinity_un'

/* ITGAVB5 binds and activates LAP-TGFB1.*/
'TGFB1(state~latent)_LTBP1_ITGAVB5' TGFB1(state{latent},w[1],RGD[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB5(x[.]) -> TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB5(x[2]) | 1 r54 @ 'normal_affinity_bin'
/* In order to have a ternary complex stable, the dissociation rule include four rules : two rules for the dissociation of LAP_TGFB1,LTBP1 which must be strong when LAP-TGFB1 is not associated with ITGAVB5 and weak when LAP-TGFB1 is associated with ITGAVB5_and two rules for the dissociation of ITGAVB5,LAP-TGFB1 which must be strong when LAP-TGFB1 is not associated with LTBP1 and weak when LAP-TGFB1 is associated with LTBP1.*/
/*'TGFB1(state~latent,nonITG)_LTBP1_dissociate' TGFB1(state~latent,w!1,RGD), LTBP1(TB!1,hinge!_,Nter!_,Cter!_) -> TGFB1(state~latent,w,RGD), LTBP1(TB,hinge!_,Nter!_,Cter!_) | 1 r55 @ 'high_affinity_un' /*NOTE: already described*/*/
'TGFB1(state~latent,nonLTBP)_ITGAVB5_dissociate' TGFB1(state{latent},w[.],RGD[2]), ITGAVB5(x[2]) -> TGFB1(state{latent},w[.],RGD[.]), ITGAVB5(x[.]) | 1 r56 @ 'high_affinity_un'
'TGFB1(state~latent,ITGAVB5)_LTBP1_dissociate' TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB5(x[2]) -> TGFB1(state{latent},w[.],RGD[2]), LTBP1(TB[.],hinge[_],Nter[_],Cter[_]), ITGAVB5(x[2]) | 1 r57 @ 'low_affinity_un'
'TGFB1(state~latent)_LTBP1_ITGAVB5_dissociate' TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB5(x[2]) -> TGFB1(state{latent},w[1],RGD[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB5(x[.]) | 1 r58 @ 'low_affinity_un'
'TGFB1(state~latent)_LTBP1_ITGAVB5_Activate' LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB5(x[2]), TGFB1(state{latent},w[1],RGD[2]),. -> LTBP1(TB[.],hinge[_],Nter[_],Cter[_]), ITGAVB5(x[.]), TGFB1(state{active},w[.],RGD[.]), iterateTGFB1() | 1 r59 @ 'high_affinity_un'

/* ITGAVB3 binds and activates lAP-TGFB1*/
'TGFB1(state~latent)_LTBP1_ITGAVB3' TGFB1(state{latent},w[1],RGD[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB3(x[.]) -> TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB3(x[2]) | 1 r60 @ 'normal_affinity_bin'
/* In order to have a ternary complex stable, the dissociation rule include four rules : two rules for the dissociation of LAP_TGFB1,LTBP1 which must be strong when LAP-TGFB1 is not associated with ITGAVB6 and weak when LAP-TGFB1 is associated with ITGAVB3_and two rules for the dissociation of ITGAVB3,LAP-TGFB1 which must be strong when LAP-TGFB1 is not associated with LTBP1 and weak when LAP-TGFB1 is associated with LTBP1.*/
/*'TGFB1(state~latent,nonITG)_LTBP1_dissociate' TGFB1(state~latent,w!1,RGD), LTBP1(TB!1,hinge!_,Nter!_,Cter!_) -> TGFB1(state~latent,w,RGD), LTBP1(TB,hinge!_,Nter!_,Cter!_) | 1 r61 @ 'high_affinity_un' /*NOTE: already described*/*/
'TGFB1(state~latent,nonLTBP)_ITGAVB3_dissociate' TGFB1(state{latent},w[.],RGD[2]), ITGAVB3(x[2]) -> TGFB1(state{latent},w[.],RGD[.]), ITGAVB3(x[.]) | 1 r62 @ 'high_affinity_un'
'TGFB1(state~latent,ITGAVB3)_LTBP1_dissociate' TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB3(x[2]) -> TGFB1(state{latent},w[.],RGD[2]), LTBP1(TB[.],hinge[_],Nter[_],Cter[_]), ITGAVB3(x[2]) | 1 r63 @ 'low_affinity_un'
'TGFB1(state~latent)_LTBP1_ITGAVB3_dissociate' TGFB1(state{latent},w[1],RGD[2]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB3(x[2]) -> TGFB1(state{latent},w[1],RGD[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB3(x[.]) | 1 r64 @ 'low_affinity_un'
'TGFB1(state~latent)_LTBP1_ITGAVB3_Activate' LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), ITGAVB3(x[2]), TGFB1(state{latent},w[1],RGD[2]),. -> LTBP1(TB[.],hinge[_],Nter[_],Cter[_]), ITGAVB3(x[.]), TGFB1(state{active},w[.],RGD[.]), iterateTGFB1() | 1 r65 @ 'normal_affinity_un'

/* FBN1 binds to integrins. FROM [12807887], cell adhesion to FBN1 and microfibrils is mediated by α5β1 and αVβ3 integrins. FROM [17158881], Human FBN1, contains TGF-binding protein-like (TB) domains and TB4 contains a flexible RGD loop that mediates cell adhesion via αVβ3 and α5β1 integrins. This study identifies integrin αVβ6 as a novel cellular receptor for fibrillin-1 with a Kd of ∼0.45 μM. Quantitative studies on αVβ3-mediated interactions with FBN1 identified a high affinity interaction (Kd ∼ 0.04 μM). Furthermore, our data suggest α5β1 is a low affinity FBN1 receptor (Kd > 1 μM).NOTE: the role of such interaction in the activation of LAP-TGB1 is not described, howevever x sites from integrins that interact with RGD are also implicated in binding to LAP-TGFB1 leading to competition.*/
'FBN1_ITGAVB6' FBN1(RGD[.]), ITGAVB6(x[.]) -> FBN1(RGD[1]), ITGAVB6(x[1]) | 1 r66 @ 'normal_affinity_bin'
'FBN1_ITGAVB6_dissociate' FBN1(RGD[1]), ITGAVB6(x[1]) -> FBN1(RGD[.]), ITGAVB6(x[.]) | 1 r67 @ 'high_affinity_un'
'FBN1_ITGAVB3' FBN1(RGD[.]), ITGAVB3(x[.]) -> FBN1(RGD[1]), ITGAVB3(x[1]) | 1 r68 @ 'high_affinity_bin'
'FBN1_ITGAVB3_dissociate' FBN1(RGD[1]), ITGAVB3(x[1]) -> FBN1(RGD[.]), ITGAVB3(x[.]) | 1 r69 @ 'low_affinity_un'
'FBN1_ITGA5B1' FBN1(RGD[.]), ITGA5B1(x[.]) -> FBN1(RGD[1]), ITGA5B1(x[1]) | 1 r70 @ 'low_affinity_bin'
'FBN1_ITGA5B1_dissociate' FBN1(RGD[1]), ITGA5B1(x[1]) -> FBN1(RGD[.]), ITGA5B1(x[.]) | 1 r71 @ 'normal_affinity_un'

/* ADAMTSL2 regulates TGF-beta signalling. FROM [18677313],ADAMTSL2 mutations in geleophysic dysplasia demonstrate a role for ADAMTS-like proteins in TGF-beta bioavailability regulation. Yeast two-hybrid screen showed that ADAMTSL2 interacts with LTBP1. Functional studies in HEK293 cells showed that ADAMTSL2 mutations lead to reduced secretion of the mutated proteins.A significant increase in total and active TGF-beta in the culture medium as well as nuclear localization of phosphorylated SMAD2 in fibroblasts from individuals with geleophysic dysplasia*/
'ADAMTSL2_LTBP1' ADAMTSL2(y[.]), LTBP1(TB[_],y[.]) -> ADAMTSL2(y[1]), LTBP1(TB[_],y[1]) | 1 r72 @ 'normal_affinity_bin'
'ADAMTSL2_LTBP1_dissociate' ADAMTSL2(y[1]), LTBP1(TB[_],y[1]) -> ADAMTSL2(y[.]), LTBP1(TB[_],y[.]) | 1 r73 @ 'high_affinity_un'

/* ADAMTSL2 and ADAMTSL3 binds to FBN1. FROM [21683322] a direct interaction between ADAMTSL2 and FBN1 was demonstrated using SPR. Although the fibrillin construct used did not contain TB5 domain, this analysis identified a specific direct interaction (KD = 60 nM= 0.06 μM) between the two proteins. FROM [22242013], similar to ADAMTSL-6, ADAMTSL-2, -3, and papilin polypeptides interacted with the N-terminal half of fibrillin-1 (the WMS domain), but ADAMTSL-1 did not. FROM [19940141] Surface plasmon resonance analyses demonstrated that ADAMTSL-6beta binds to the N-terminal half of fibrillin-1 with high affinity (Kd= 80 nM) and ADAMTSL6 promotes fibrillin-1 fibril formation. FROM [22242013] a new mutation found in family with Weill-Marchesani syndrome (WMS)that deletes three domains in fibrillin-1, abolishing a binding site utilized by ADAMTSLIKE-2, -3, -6, and papilin is associated with thick skin, short stature, and brachydactyly when replicated in mice.There is an upregulation of collagen genes in the skin of WMΔ mice and increased Trichrome staining of WMΔ dermis suggested increased TGFβ signaling. In addition, molecular interactions of LTBP1 with both ADAMTSL-2 and ADAMTSL-3 were determined, suggesting that loss of the ADAMTSL binding site in WMΔ mutant mice might render the large latent TGFβ complex more susceptible to activation. However, if activation of TGFβ signaling underlies the dermal fibrosis in WMΔ mutant mice, this activation of signaling did not manifest detectable differences in other conventional TGFβ signaling readouts (e.g., increased α-smooth muscle actin positive cells). It is possible that local changes in the fibrillin microfibril matrix could influence force-dependent activation of latent TGFβ*/
'ADAMTSL2_FBN1' ADAMTSL2(x[.]), FBN1(WMS[.]) -> ADAMTSL2(x[1]), FBN1(WMS[1]) | 1 r74 @ 'high_affinity_bin'
'ADAMTSL2_FBN1_dissociate' ADAMTSL2(x[1]), FBN1(WMS[1])-> ADAMTSL2(x[.]), FBN1(WMS[.]) | 1 r75 @ 'very_high_affinity_un'
'ADAMTSL3_FBN1' ADAMTSL3(x[.]), FBN1(WMS[.]) -> ADAMTSL3(x[1]), FBN1(WMS[1]) | 1 r76 @ 'normal_affinity_bin'
'ADAMTSL3_FBN1_dissociate' ADAMTSL3(x[1]), FBN1(WMS[1]) -> ADAMTSL3(x[.]), FBN1(WMS[.]) | 1 r77 @ 'high_affinity_un'
'ADAMTSL3_LTBP1' ADAMTSL3(y[.]), LTBP1(TB[_],y[.]) -> ADAMTSL3(y[1]), LTBP1(TB[_],y[1]) | 1 r78 @ 'normal_affinity_bin'
'ADAMTSL3_LTBP1_dissociate' ADAMTSL3(y[1]), LTBP1(TB[_],y[1]) -> ADAMTSL3(y[.]), LTBP1(TB[_],y[.]) | 1 r79 @ 'high_affinity_un'
'ADAMTSL6_FBN1' ADAMTSL6(x[.]), FBN1(WMS[.]) -> ADAMTSL6(x[1]), FBN1(WMS[1]) | 1 r80 @ 'high_affinity_bin'
'ADAMTSL6_FBN1_dissociate' ADAMTSL6(x[1]), FBN1(WMS[1]) -> ADAMTSL6(x[.]), FBN1(WMS[.]) | 1 r81 @ 'very_high_affinity_un'

'ADAMTSL2_increasestoringLTBP1_TGF' ADAMTSL2(y[1],x[.]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[.]) -> ADAMTSL2(y[1],x[2]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[2]) | 1 r82 @ 'very_high_affinity_bin'
'ADAMTSL2_increasestoringLTBP1_TGF_dissociate' ADAMTSL2(y[1],x[2]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[2]) -> ADAMTSL2(y[1],x[.]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[.]) | 1 r83 @ 'low_affinity_un'
'ADAMTSL2_increasestoringLTBP1_TGF_dissociate_bis' ADAMTSL2(y[1],x[2]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[2]) -> ADAMTSL2(y[.],x[2]), LTBP1(TB[_],y[.]), FBN1(Cter[_],WMS[2]) | 1 r84 @ 'low_affinity_un'

'ADAMTSL3_increasestoringLTBP1_TGF' ADAMTSL3(y[1],x[.]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[.]) -> ADAMTSL3(y[1],x[2]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[2]) | 1 r85 @ 'very_high_affinity_bin'
'ADAMTSL3_increasestoringLTBP1_TGF_dissociate' ADAMTSL3(y[1],x[2]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[2]) -> ADAMTSL3(y[1],x[.]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[.]) | 1 r86 @ 'low_affinity_un'
'ADAMTSL3_increasestoringLTBP1_TGF_dissociate_bis' ADAMTSL3(y[1],x[2]), LTBP1(TB[_],y[1]), FBN1(Cter[_],WMS[2]) -> ADAMTSL3(y[.],x[2]), LTBP1(TB[_],y[.]), FBN1(Cter[_],WMS[2]) | 1 r87 @ 'low_affinity_un'

/* ADAMTS10 binds to FBN1. FROM [21402694], ADAMTS10 protein interacts with fibrillin-1 and promotes its deposition in extracellular matrix of cultured fibroblasts.Using ligand affinity blotting and surface plasmon resonance, recombinant ADAMTS10 was found to bind to fibrillin-1 with a high degree of specificity and with high affinity. Two sites of ADAMTS10 binding to fibrillin-1 were identified, one toward the N terminus (SPR for rF11,full N-ter KD= 28nM)and another in the C-terminal half of fibrillin-1 (SPR for rF6, full Cter no data , for rF36 Kd=72nM). DONC VALIDATION QUE DU Nter AVEC LA MOLECULE ENTIERE. FROM [22242013], using SPR interaction studies, a KD of 450 nM was calculated for the binding of the N-terminal fibrillin-1 polypeptide to the C-terminal end of ADAMTS-10.A ternary complex of ADAMTSL, ADAMTS-10, and fibrillin-1 has been proposed and the proximity of the RGD-containing domain to the ADAMTSL binding site in fibrillin-1 suggests that integrins may cooperate with ADAMTSL proteins and ADAMTS-10 in modulating fibrillin microfibril [19940141,21402694]*/
'ADAMTS10_FBN1(Nter)' ADAMTS10(x[.]), FBN1(Nter[.]) -> ADAMTS10(x[1]), FBN1(Nter[1]) | 1 r88 @ 'high_affinity_bin'
'ADAMTS10_FBN1(Nter)_dissociate' ADAMTS10(x[1]), FBN1(Nter[1]) -> ADAMTS10(x[.]), FBN1(Nter[.])  | 1 r89 @ 'very_high_affinity_un'
'ADAMTS10_increasestoringLTBP1_TGF' ADAMTS10(x[1]), FBN1(Nter[1],Cter[_],Hyb1[.]), LTBP1(TB[_],Cter[.]) -> ADAMTS10(x[1]), FBN1(Nter[1],Cter[_],Hyb1[2]), LTBP1(TB[_],Cter[2])  | 1 r90 @ 'very_high_affinity_bin'
'ADAMTS10_increasestoringLTBP1_TGF_dissociate' ADAMTS10(x[1]), FBN1(Nter[1],Cter[_],Hyb1[2]), LTBP1(TB[_],Cter[2]) -> ADAMTS10(x[1]), FBN1(Nter[1],Cter[_],Hyb1[.]), LTBP1(TB[_],Cter[.]) | 1 r91 @ 'normal_affinity_un'
'ADAMTS10_increasestoringLTBP1_TGF_dissociate_bis' ADAMTS10(x[1]), FBN1(Nter[1],Cter[_],Hyb1[2]), LTBP1(TB[_],Cter[2]) -> ADAMTS10(x[.]), FBN1(Nter[.],Cter[_],Hyb1[2]), LTBP1(TB[_],Cter[2]) | 1 r92 @ 'normal_affinity_un'
/*le Cter!_ implique que FBN1 soit lié à FN1 (seul interactant déclaré)*/


/* ADAMTSL5 binds to fibrillin-1 and co-localized with fibrillin microfibrils in the extracellular matrix of cultured fibroblasts, but without discernible effect on microfibril assembly[23010571]. Because of the non demonstrated implication in TGF-b storage and release, I do not keep this interaction*/

/* ADAMTS1 binds Fibulin. FROM [16061471], Fibulin-1 acts as a cofactor for the matrix metalloprotease ADAMTS-1. A yeast two-hybrid screen was performed using the carboxyl-terminal region of ADAMTS-1 as bait and fibulin-1 was identified as a potential interacting molecule. The sequence corresponds to the carboxyl-end of fibulin-1D (amino acid residues 476-703), including the two last epidermal growth factor-like elements and the carboxyl-terminal fibulin-type module. Fibulin-1 was found to enhance the capacity of ADAMTS-1 to cleave aggrecan, a proteoglycan known to bind to fibulin-1. We confirmed that fibulin-1 was not a proteolytic substrate for ADAMTS-1.*/
'ADAMTS1_FBLN1' ADAMTS1(x[.]), FBLN1(Cter[.]) -> ADAMTS1(x[1]), FBLN1(Cter[1]) | 1 r93 @ 'normal_affinity_bin'
'ADAMTS1_FBLN1_dissociate' ADAMTS1(x[1]), FBLN1(Cter[1])-> ADAMTS1(x[.]), FBLN1(Cter[.]) | 1 r94 @ 'high_affinity_un'

/* ADAMTS1 binds and activates LAP_TGFB1. The THBS1-Containing Domain of ADAMTS1 Mediates Interactions With the Latent Form of TGF-Beta, LAP-TGF-Beta. Similar to thrombospondin/TGF-Beta interactions, ADAMTS1 interacts via its KTFR with LSKL in the LAP-TGF-Beta [21826695]. Note that even we did not demonstrate LTBP1 requirement, we hypothesize that in the liver fibrosis context (Stellate cells) LAP-TGFB1 is secreted with LTBP1.*/
'ADAMTS1_TGFB1(state~latent)' ADAMTS1(KTFR[.]), TGFB1(state{latent},LSKL[.]) -> ADAMTS1(KTFR[1]), TGFB1(state{latent},LSKL[1]) | 1 r95 @ 'normal_affinity_bin'
'ADAMTS1_TGFB1(state~latent)_dissociate' ADAMTS1(KTFR[1]), TGFB1(state{latent},LSKL[1]) -> ADAMTS1(KTFR[.]), TGFB1(state{latent},LSKL[.]) | 1 r96 @ 'high_affinity_un'
'ADAMTS1_TGFB1(state~latent)_Activate' ADAMTS1(KTFR[1]), TGFB1(state{latent},LSKL[1]),. -> ADAMTS1(KTFR[.]), TGFB1(state{active},LSKL[.]), iterateTGFB1() | 1 r97 @ 'normal_affinity_un'

/* ADAMTS4 binds FN1. FROM [15161923], ADAMTS4 (aggrecanase-1) interaction with the C-terminal domain of fibronectin inhibits proteolysis of aggrecan. A yeast two-hybrid assay and solid-phase binding assay using wild-type fibronectin and ADAMTS4 and their mutants demonstrated that the C-terminal domain of fibronectin is capable of binding to the C-terminal spacer domain of ADAMTS4.*/
'ADAMTS4_FN1' ADAMTS4(spacer[.]), FN1(Cter[.]) -> ADAMTS4(spacer[1]), FN1(Cter[1]) | 1 r98 @ 'normal_affinity_bin'
'ADAMTS4_FN1_dissociate' ADAMTS4(spacer[1]), FN1(Cter[1]) -> ADAMTS4(spacer[.]), FN1(Cter[.]) | 1 r99 @ 'high_affinity_un'

/* MMP dependent activation of LAP-TGFB1. FROM [17015622], We provide in vitro and in vivo evidence that BMP1 cleaves LTBP1 at two specific sites, thus liberating LLC from ECM and resulting in consequent activation of TGFβ1 via cleavage of LAP by non–BMP1-like proteinases. In mouse embryo fibroblasts, LAP cleavage is shown to be predominantly MMP2 dependent. MMP2 is capable of in vitro cleavage of LAP in small latent complexes (SLC), but MMP2 cleavage of LLC-associated LAP was extremely inefficient, without prior incubation of LLC with BMP1. Thus, in vitro, as in vivo, prior LLC processing by BMP1 greatly facilitates subsequent MMP2 cleavage of LAP.*/
'BMP1_LTBP1-TGFB1(state~latent)' LTBP1(TB[_],Nter[_],Cter[_],hinge[_],x[.]), BMP1(Cat[.]) -> LTBP1(TB[_],Nter[_],Cter[_],hinge[_],x[1]), BMP1(Cat[1]) | 1 r100 @ 'high_affinity_bin'
'BMP1_LTBP1-TGFB1(state~latent)_dissociate' LTBP1(TB[_],Nter[_],Cter[_],hinge[_],x[1]), BMP1(Cat[1])-> LTBP1(TB[_],Nter[_],Cter[_],hinge[_],x[.]), BMP1(Cat[.]) | 1 r101 @ 'very_high_affinity_un'
'BMP1_LTBP1-TGFB1(state~latent)-MMP2' TGFB1(state{latent},w[1],z[.]), LTBP1(TB[1],Nter[_],Cter[_],hinge[_],x[2]), BMP1(Cat[2]), MMP2(state{active},Cat[.]) -> TGFB1(state{latent},w[1],z[3]), LTBP1(TB[1],Nter[_],Cter[_],hinge[_],x[2]), BMP1(Cat[2]), MMP2(state{active},Cat[3]) | 1 r102 @ 'high_affinity_bin'
'BMP1_LTBP1-TGFB1(state~latent)-MMP2_dissociate' TGFB1(state{latent},w[1],z[3]), LTBP1(TB[1],Nter[_],Cter[_],hinge[_],x[2]), BMP1(Cat[2]), MMP2(state{active},Cat[3]) -> TGFB1(state{latent},w[1],z[.]), LTBP1(TB[1],Nter[_],Cter[_],hinge[_],x[2]), BMP1(Cat[2]), MMP2(state{active},Cat[.]) | 1 r103 @ 'normal_affinity_un'
'BMP1_LTBP1-TGFB1(state~latent)-MMP2_Activate' LTBP1(TB[1],Nter[_],Cter[_],hinge[_],x[2]), BMP1(Cat[2]), MMP2(state{active},Cat[3]), TGFB1(state{latent},w[1],z[3]),. -> LTBP1(TB[.],Nter[_],Cter[_],hinge[_],x[.]), BMP1(Cat[.]), MMP2(state{active},Cat[.]), TGFB1(state{active},w[.],z[.]), iterateTGFB1() | 1 r104 @ 'high_affinity_un'

/* MMP9 and CD44 in activation of latentTGF ? FROM [18243766 revue] MMP2 and MMP9 are both able to cleave the latency associated peptide of TGF-β, releasing active TGF-β (10652271; 17015622). This may be important in osteoblast survival (12226090) and atherosclerosis (16690877). In vitro MMP2 and MMP9 preferentially cleave TGF-β2 and TGF-β3 with little effect on TGF-β1. Furthermore, MMP9 activation of TGF-β is dependent on CD44 in vitro (10652271). Over-expression of MMP2 and MMP9 occurs in caveolin null mice and this gives rise to an invasive breast tumour phenotype associated with increased TGF-β activation (16400031). Mice null for MMP9 (mmp9−/−) mice over-expressing IL13 show a partial reduction in TGF-β1 activity compared with wild type IL-13 transgenic mice, suggesting MMP9 is involved in TGF-β activation in vivo, however this does not rely on the presence of CD44 (11560996). FROM [10652271], CD44 has been shown to act as a docking molecule for MMP9, localizing MMP9 activity to the cell surface. FROM [11935029] CD44KO mice had normal to enhanced levels of latent TGFβ but reduced levels of active TGFβ in broncho-alveolar lavage fluid, suggesting that CD44 may be critical for regulating TGFβ activation. FROM [18397995] CD44KO fibroblasts exhibit a defect in TGFβ activation that translates into a decrease in TGFβ-receptor mediated signaling and the robust activation of TGFβ induced in CD44WT cells in response to wounding was reduced to near background (uninjured) levels in the presence of the MMP inhibitor. NOTE: CD44 binds to TGFBR1 with high affinitty [12145287] thereby promoting TGFB1 signaling and CD44 binds also ECM componenents that include laminin, fibronectin and collagen (For review on CD44, see [25081532]).*/

'CD44_MMP9' CD44(x[.]), MMP9(a[.]) -> CD44(x[1]), MMP9(a[1]) | 1 r105 @ 'normal_affinity_bin'
'CD44_MMP9_dissociate' CD44(x[1]), MMP9(a[1]) -> CD44(x[.]), MMP9(a[.]) | 1 r106bis @ 'high_affinity_un'
'CD44_MMP9_TGFB1(state~latent)_LTBP1__FN1_FBN1' TGFB1(state{latent},w[1],d[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), CD44(x[2]), MMP9(a[2],Cat[.]) -> TGFB1(state{latent},w[1],d[3]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), CD44(x[2]), MMP9(a[2],Cat[3]) | 1 r106 @ 'normal_affinity_bin'
'CD44_MMP9_TGFB1(state~latent)_LTBP1__FN1_FBN1_dissociate' TGFB1(state{latent},w[1],d[3]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), CD44(x[2]), MMP9(a[2],Cat[3]) -> TGFB1(state{latent},w[1],d[.]), LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), CD44(x[2]), MMP9(a[2],Cat[.]) | 1 r107 @ 'high_affinity_un'
'CD44_MMP9_TGFB1(state~latent)_LTBP1__FN1_FBN1_Activate' LTBP1(TB[1],hinge[_],Nter[_],Cter[_]), CD44(x[2]), MMP9(a[2],Cat[3]), TGFB1(state{latent},w[1],d[3]),. -> LTBP1(TB[.],hinge[_],Nter[_],Cter[_]), CD44(x[.]), MMP9(a[.],Cat[.]), TGFB1(state{active},w[.],d[.]), iterateTGFB1() | 1 r108 @ 'high_affinity_un'

/* MMP14 cleaves CD44. FROM [11381077], MT1-MMP acts as a processing enzyme for CD44, releasing it into the medium as a soluble 70-kD fragment. Furthermore, this processing event stimulates cell motility; however, expression of either CD44 or MT1-MMP alone did not stimulate cell motility. FROM [19243022] TGF-beta induces CD44 cleavage that promotes migration of MDA-MB-435s cells through the up-regulation of MT1-MMP*/
'CD44_MMP14' CD44(y[.]), MMP14(state{active},Cat[.]) -> CD44(y[1]), MMP14(state{active},Cat[1]) | 1 r108bis @ 'normal_affinity_bin'
'CD44_MMP14_dissociate' CD44(y[1]), MMP14(state{active},Cat[1]) -> CD44(y[.]), MMP14(state{active},Cat[.]) | 1 r109 @ 'high_affinity_un'
'CD44_MMP14-cleavage' MMP14(state{active},Cat[1]), CD44(y[1]) -> MMP14(state{active},Cat[.]),. | 1 r110 @ 'normal_affinity_un'

/* ADAM12 and αVβ3 integrin in MMP14 activation. FROM [24006261], a ternary protein complex composed of ADAM12, αVβ3 integrin and MMP14 at the tumor cell surface regulates the function of MMP14. Neither the catalytic activity of ADAM12 nor the cytoplasmic tail of ADAM12 were required. We have previously shown that the secreted form of ADAM12 binds αVβ3 integrin on tumor cells [16213489]. Moreover, it is well established that MMP14 associates with αVβ3 integrin at the cell surface, and that the cell surface MMP14 activity can be regulated by αVβ3 integrin [17175151,15260832, 12427871).ADAM12 coimmunoprecipitated with both MMP14 and αVβ3 integrin. A direct or indirect interaction between ADAM12 and MMP14 occurs in the lysate of cells ; however, αVβ3 integrin seems to be the key-player for correct transport of the protein triple complex to the cell surface. FROM [12427871] the biochemical association of MT1-MMP with β1 or αvβ3 integrins was directly assessed by coimmunoprecipitation assays.*/
'MMP14,ITGAVB3' MMP14(state{latent},a[.]), ITGAVB3(z[.]) -> MMP14(state{latent},a[1]), ITGAVB3(z[1]) | 1 r111 @ 'normal_affinity_bin'
'ADAM12,ITGAVB3' ADAM12(x[.]), ITGAVB3(w[.]) -> ADAM12(x[1]), ITGAVB3(w[1]) | 1 r112 @ 'normal_affinity_bin'
'MMP14,ITGAVB3_dissociate' MMP14(state{latent},a[1]), ITGAVB3(z[1]) -> MMP14(state{latent},a[.]), ITGAVB3(z[.]) | 1 r113 @ 'high_affinity_un'
'ADAM12,ITGAVB3_dissociate' ADAM12(x[1]), ITGAVB3(w[1]) -> ADAM12(x[.]), ITGAVB3(w[.]) | 1 r114 @ 'high_affinity_un'
'MMP14,ADAM12,ITGAVB3' MMP14(state{latent},a[1]), ITGAVB3(z[1],w[.]), ADAM12(x[.]) -> MMP14(state{latent},a[1]), ITGAVB3(z[1],w[2]), ADAM12(x[2]) | 1 r115 @ 'normal_affinity_bin'
'MMP14,ADAM12,ITGAVB3_dissociate' MMP14(state{latent},a[1]), ITGAVB3(z[1],w[2]), ADAM12(x[2]) -> MMP14(state{latent},a[1]), ITGAVB3(z[1],w[.]), ADAM12(x[.]) | 1 r116 @ 'low_affinity_un'
'MMP14 activation' MMP14(state{latent},a[1]), ITGAVB3(z[1],w[2]), ADAM12(x[2]) -> MMP14(state{active},a[.]), ITGAVB3(z[.],w[.]), ADAM12(x[.]) | 1 r117 @ 'high_affinity_un'

/* Binding of fibronectin to type I collagen. FROM [23653354],it has long been known that FN is crucial for fibroblast attachment to collagen matrices and for organization of collagen type I fibrils. In vitro, however, 6FnI1–2FnII7–9FnI binds strongly to gelatin, the denatured form of collagen, but not to triple-helical collagen fibrils; 6FnI1–2FnII7–9FnI was thus named the “gelatin-binding domain” (GBD). To reconcile the in vitro and cellular findings, it was suggested that the physiological function of the FN-collagen interaction is related to clearance of denatured collagenous material during wound repair and binding of exposed single collagen chains following fiber processing by MMP during tissue growth. However, recent work suggested that the collagen triple helix unfolds locally at physiological temperatures, which suggested the possibility that FN could also interact with unwound collagen in intact fibers. Here result indicates that the GBD does not undergo any major conformational changes upon collagen binding. Rather, we propose that the FN GBD adopts in solution a well defined major conformation, which is capable and ready for functional engagement with collagen. FROM [12145303] collagen deposition is regulated by the cells, both indirectly through integrin alpha(5)beta(1)-dependent polymerization of fibronectin and directly through collagen-binding integrins.FROM [17928541]fibronectin-dependent collagen I deposition regulates the cell migratory response to fibronectin*/
'FN1_COL1' FN1(c[.]), COL1(y[.]) -> FN1(c[1]), COL1(y[1]) | 1 r118 @ 'normal_affinity_bin'
'FN1_COL1_dissociate' FN1(c[1]), COL1(y[1]) -> FN1(c[.]), COL1(y[.]) | 1 r119 @ 'high_affinity_un'
/* Binding of alha2Beta1 Integrin to type I collagen. FROM [2156854] the alha2Beta1 Integrin Cell Surface Collagen Receptor Binds to the al(I)-CB3 Peptide of Collagen*/
'ITGA2B1_COL1' ITGA2B1(x[.]), COL1(z[.]) -> ITGA2B1(x[1]), COL1(z[1]) | 1 r120 @ 'normal_affinity_bin'
'ITGA2B1_COL1_dissociate' ITGA2B1(x[1]), COL1(z[1]) -> ITGA2B1(x[.]), COL1(z[.]) | 1 r121 @ 'high_affinity_un'

/* Collagen1 activates pro-MMP2 through activation of integrin beta 1 [1322793,10197640,10421655]*/
'COL1_MMP2_ITGA21' ITGA2B1(x[1]), COL1(z[1],c[.]), MMP2(state{latent},CBD[.]) -> ITGA2B1(x[1]), COL1(z[1],c[2]), MMP2(state{latent},CBD[2]) | 1 r122 @ 'normal_affinity_bin'
'COL1_MMP2_ITGA2B1_dissociate' ITGA2B1(x[1]), COL1(z[1],c[2]), MMP2(state{latent},CBD[2]) -> ITGA2B1(x[1]), COL1(z[1],c[.]), MMP2(state{latent},CBD[.]) | 1 r123 @ 'low_affinity_un'
'MMP2 Activation ' ITGA2B1(x[1]), COL1(z[1],c[2]), MMP2(state{latent},CBD[2]) -> ITGA2B1(x[.]), COL1(z[.],c[.]), MMP2(state{active},CBD[.]) | 1 r124 @ 'high_affinity_un'

/* Degradation of fibrillin by MMP. FROM [10386589] and [10229672] Cleavage sites for MMP2, MMP9 and MMP14 were identified at the carboxy-terminal end of the recombinant fibrillin. Such cleavage is predicted to disrupt the structure and function of the fibrillin-rich microfibrils.*/
'FBN1_MMP2' FBN1(Cter[.]), MMP2(state{active},Cat[.]) -> FBN1(Cter[1]), MMP2(state{active},Cat[1]) | 1 r125 @ 'normal_affinity_bin'
'FBN1_MMP2_dissociate' FBN1(Cter[1]), MMP2(state{active},Cat[1]) -> FBN1(Cter[.]), MMP2(state{active},Cat[.]) | 1 r126 @ 'high_affinity_un'
'FBN1_MMP2_degradation' MMP2(state{active},Cat[1]), FBN1(Cter[1]) -> MMP2(state{active},Cat[.]),. | 1 r127 @ 'high_affinity_un'
'FBN1_MMP14' FBN1(Cter[.]), MMP14(state{active},Cat[.]) -> FBN1(Cter[1]), MMP14(state{active},Cat[1]) | 1 r128 @ 'normal_affinity_bin'
'FBN1_MMP14_dissociate' FBN1(Cter[1]), MMP14(state{active},Cat[1]) -> FBN1(Cter[.]), MMP14(state{active},Cat[.]) | 1 r129 @ 'high_affinity_un'
'FBN1_MMP14_degradation' MMP14(state{active},Cat[1]), FBN1(Cter[1]) -> MMP14(state{active},Cat[.]),. | 1 r130 @ 'high_affinity_un'
'FBN1_MMP9' FBN1(Cter[.]), MMP9(Cat[.]) -> FBN1(Cter[1]), MMP9(Cat[1]) | 1 r131 @ 'normal_affinity_bin'
'FBN1_MMP9_dissociate' FBN1(Cter[1]), MMP9(Cat[1]) -> FBN1(Cter[.]), MMP9(Cat[.]) | 1 r132 @ 'high_affinity_un'
'FBN1_MMP9_degradation' MMP9(Cat[1]), FBN1(Cter[1]) -> MMP9(Cat[.]),. | 1 r133 @ 'high_affinity_un'

/* Binding of THBS1 to type I collagen. FROM [3571333] Binding of thrombospondin (TSP) to types I-V collagen was examined by direct binding assays using 125I-TSP and by visualization of rotary-shadowed intermolecular complexes in the electron microscope. The binding of TSP was highest to type V collagen in the absence of Ca, while lower but significant levels of binding were observed to all other collagen types.*/
'THBS1_COL1' THBS1(a[.]), COL1(b[.]) -> THBS1(a[1]), COL1(b[1]) | 1 r134 @ 'low_affinity_bin'
'THBS1_COL1_dissociate' THBS1(a[1]), COL1(b[1]) -> THBS1(a[.]), COL1(b[.]) | 1 r135 @ 'normal_affinity_un'

/* Binding of Decorin to type I collagen [2635760,2496122]. FROM [25661523]The name decorin originates from its ability to decorate collagen type I fibrils. Decorin has been shown to bind to the d and e bands of type I collagen via its core protein, “decoron,” thereby controlling fibril formation [1449470], [19753304] and [20080181].*/
'DCN(high)_COL1' DCN(core[.]), COL1(a[.]) -> DCN(core[1]), COL1(a[1]) | 1 r136 @ 'high_affinity_bin'
'DCN_COL1_dissociate' DCN(core[1]), COL1(a[1]) -> DCN(core[.]), COL1(a[.]) | 1 r137 @ 'very_high_affinity_un'

/* Binding of Decorin to TGFB1. FROM [2374594] Decorins bind to TGFB1, an autocrine factor that stimulates cell growth. As TGFB1 induces synthesis of decorin in many cell types, decorin may be a component of a feedback system regulating cell growth. FROM [7798269] Scatchard analysis of the binding of 125I-TGF-beta 1 to immobilized decorin purified from osteoblastic MC3T3-E1 cell conditioned medium revealed that there were two specific binding sites with high and low affinities for TGF-beta 1 (Kd = 0.3 and 5 nM, respectively). The addition of decorin along with TGF-beta 1 enhanced the inhibitory effect of TGF-beta 1 on MC3T3-E1 cell proliferation. FROM [8093006] affinity measurements suggest a two-site binding model with Kd values ranging from 1 to 20 nM for a high-affinity binding site and 50 to 200 nM for the lower-affinity binding site. The stoichiometry indicated that the high-affinity binding site was present in one of ten proteoglycan core molecules and that each molecule contained a low-affinity binding site. Latent recombinant TGF-beta 1 precursor bound slightly to fibromodulin and not at all to decorin and biglycan. FROM [9675033] decorin contains independent binding sites for TGFB1 and type I collagen fibrils.The high-affinity binding sites of decorin for type I collagen and TGFB1 are different, whereas lower affinity binding sites could be identical. # FROM [9148753] Recombinant MMP2 digest DCN into seven major fragments. MMP-2 liberate TGF-β1 from the DCN–TGF-β1 complex. No proteolytic degradation of TGF-β1 released from the complex was observed by SDS\PAGE. It may be reasonable to speculate that the released growth factor is functionally active. NOTE: DCN Binds to TGFB1 via two sites (low et high affinity). THe low site may be shared with binding to COL1 (the strong interaction COL1-DCN involves the core domain of thereby leading to the hypothesis of two binding site for COL1). In the model, we keep sites c and x form DCN as low and high affinity binding sites for TGFB1 whereas core site is the high affinity site for COL1.*/
'TGFB1_DCN(high)' TGFB1(state{active},a[.]), DCN(x[.]) -> TGFB1(state{active},a[1]), DCN(x[1]) | 1 r138 @ 'high_affinity_bin'
'TGFB1_DCN(high)_dissociate' TGFB1(state{active},a[1]), DCN(x[1]) -> TGFB1(state{active},a[.]), DCN(x[.]) | 1 r139 @ 'very_high_affinity_un'
'TGFB1_DCN(low)' TGFB1(state{active},a[.]), DCN(c[.]) -> TGFB1(state{active},a[1]), DCN(c[1]) | 1 r140 @ 'low_affinity_bin'
'TGFB1_DCN(low)_dissociate' TGFB1(state{active},a[1]), DCN(c[1]) -> TGFB1(state{active},a[.]), DCN(c[.]) | 1 r141 @ 'normal_affinity_un'
'DCN_MMP2' DCN(y[.]), MMP2(state{active},Cat[.]) -> DCN(y[1]), MMP2(state{active},Cat[1]) | 1 r142 @ 'normal_affinity_bin'
'DCN_MMP2_dissociate' DCN(y[1]), MMP2(state{active},Cat[1]) -> DCN(y[.]), MMP2(state{active},Cat[.]) | 1 r143 @ 'high_affinity_un'
'TGF_DCN_MMP2' TGFB1(state{active},a[1]), DCN(x[1],y[.]), MMP2(state{active},Cat[.]) -> TGFB1(state{active},a[1]), DCN(x[1],y[2]), MMP2(state{active},Cat[2]) | 1 r144 @ 'normal_affinity_bin'
'TGF_DCN_MMP2_dissociate' TGFB1(state{active},a[1]), DCN(x[1],y[2]), MMP2(state{active},Cat[2]) -> TGFB1(state{active},a[1]), DCN(x[1],y[.]), MMP2(state{active},Cat[.]) | 1 r145 @ 'low_affinity_un'
'DCN_TGFB1_MMP2_Activate' DCN(x[1],y[2]), MMP2(state{active},Cat[2]), TGFB1(state{active},a[1]),. -> DCN(x[.],y[.]), MMP2(state{active},Cat[.]), TGFB1(state{active},a[.]), iterateTGFB1() | 1 r146 @ 'high_affinity_un'

/* Binding of Decorin to fibrillin-1 and MAGP1. FROM [10793130]MAGP1 and fibrillin-1, two protein components of extracellular microfibrils, were shown by immunoprecipitation studies to interact with the chondroitin sulfate proteoglycan decorin in the medium of cultured fetal bovine chondrocytes. Coprecipitation of the proteoglycan from chondroitinase-treated medium indicates that its association with MAGP-1 occurs via the decorin core protein. Immunoprecipitation of truncated fibrillin proteins expressed in Chinese hamster ovary (CHO) cells localized a decorin-binding site to an amino-terminal region of fibrillin-1 near the proline-rich domain.MAGP-1, in contrast, does not bind to NH2-part. Decorin interacted with each protein individually and with both proteins together to form a ternary complex.*/
'DCN_MAGP1' DCN(core[.]), MAGP1(x[.]) -> DCN(core[1]), MAGP1(x[1]) | 1 r147 @ 'normal_affinity_bin'
'DCN_MAGP1_dissociate' DCN(core[1]), MAGP1(x[1]) -> DCN(core[.]), MAGP1(x[.]) | 1 r148 @ 'high_affinity_un'
'DCN_FBN1' DCN(a[.]), FBN1(Nter[.]) -> DCN(a[1]), FBN1(Nter[1]) | 1 r149 @ 'normal_affinity_bin'
'DCN_FBN11_dissociate' DCN(a[1]), FBN1(Nter[1]) -> DCN(a[.]), FBN1(Nter[.]) | 1 r150 @ 'high_affinity_un'
'DCN_FBN1_MGAP1' DCN(core[1]), MAGP1(x[1],Cter[.]), FBN1(Nter[.],Hyb1[.]) -> DCN(core[1]), MAGP1(x[1],Cter[2]), FBN1(Nter[2],Hyb1[.]) | 1 r150bis @ 'normal_affinity_bin'
'DCN_FBN1_MGAP1_dissociate' DCN(core[1]), MAGP1(x[1],Cter[2]), FBN1(Nter[2],Hyb1[.]) -> DCN(core[1]), MAGP1(x[1],Cter[.]), FBN1(Nter[.],Hyb1[.]) | 1 r151 @ 'low_affinity_un'
/*'MAGP1_FBN1' MAGP1(Cter), FBN1(Nter,Hyb1) -> MAGP1(Cter!1), FBN1(Nter!1,Hyb1) @ 'normal_affinity_bin' /*NOTE: rule already written above*/*/

/* Binding of Decorin to Fibronectin. FROM [1747115], decorin interacts with fibronectin via its core protein. In a solid-phase assay, both high-affinity (KD values between 10 and 20 nM) and low-affinity (KD values between 110 and 130 nM) binding sites were found.*/
'FN1(a)_DCN' FN1(a[.]), DCN(core[.]) -> FN1(a[1]), DCN(core[1]) | 1 r152 @ 'high_affinity_bin'
'FN1(a)_DCN_dissociate' FN1(a[1]), DCN(core[1]) -> FN1(a[.]), DCN(core[.]) | 1 r153 @ 'very_high_affinity_un'
'FN1(b)_DCN' FN1(b[.]), DCN(core[.]) -> FN1(b[1]), DCN(core[1]) | 1 r154 @ 'low_affinity_bin'
'FN1(b)_DCN_dissociate' FN1(b[1]), DCN(core[1]) -> FN1(b[.]), DCN(core[.]) | 1 r155 @ 'normal_affinity_un'

/* Thrombospondin binds and activate LAP-TGFB1. FROM [22363291], TSP-1 (alias THBS1) can activate latent TGF-β1 in vitro [8349738,7929413) and in vivo [9657149]. Indeed TSP-1 activates recombinant small and large latent TGF-βs even in a cell-free system, and this activation is inhibited by a specific antibody for the amino-terminus of the LAP but not for the COOH terminus of the LAP and the LTBP [7929413]. Thus, TSP-1 binds small latent TGF-β and large latent TGF-β, and this binding interaction is sufficient to generate biologically active TGF-β. The two sequences (GGWSHW and KRFK) located in the type I repeat of TSP-1 comprise the region responsible for binding and activating latent TGF-β, respectively [3045564, 7929414]. In LAP, a sequence LSKL near the amino-terminus plays a pivotal role in the interaction and TSP-1-mediated activation of latent TGF-β, because LSKL peptides competitively inhibit TSP-1- or a KRFK-peptide-mediated latent TGF-β activation [10224129]. Furthermore, mutations of the LSKL sequence in LAP reduce the binding of LAP to the mature TGF-β and induce the impaired ability of LAP to confer latency to mature TGF-β [15208302]. Such a direct interaction between TSP-1 and LAP is supposed to induce a conformational change of LAP in relation to mature TGF-β and thereby presumably unmask the TGF-β receptor binding site that can bind to its receptor [10708953).FROM [5342643]The tryptophan-rich motifs of the thrombospondin type 1 repeats bind VLAL motifs in the latent transforming growth factor-beta complex*/
'THBS1_TGFB1(state~latent)' THBS1(WxxW[.],KRFK[.]), TGFB1(state{latent},VLAL[.],LSKL[.]) -> THBS1(WxxW[1],KRFK[2]), TGFB1(state{latent},VLAL[1],LSKL[2]) | 1 r156 @ 'normal_affinity_bin'
'THBS1_TGFB1(state~latent)_dissociate' THBS1(WxxW[1],KRFK[2]), TGFB1(state{latent},VLAL[1],LSKL[2]) -> THBS1(WxxW[.],KRFK[.]), TGFB1(state{latent},VLAL[.],LSKL[.]) | 1 r157 @ 'low_affinity_un'
'THBS1_TGFB1(state~latent)_Activate' THBS1(WxxW[1],KRFK[2]), TGFB1(state{latent},VLAL[1],LSKL[2]),. -> THBS1(WxxW[.],KRFK[.]), TGFB1(state{active},VLAL[.],LSKL[.]), iterateTGFB1() | 1 r158 @ 'high_affinity_un'
'THBS1_TGFB1(state~latent)_LTBP1_FN1_FBN1' TGFB1(state{latent},w[1],VLAL[.],LSKL[.]), LTBP1(TB[1]), THBS1(WxxW[.],KRFK[.]) -> TGFB1(state{latent},w[1],VLAL[2],LSKL[3]), LTBP1(TB[1]), THBS1(WxxW[2],KRFK[3]) | 1 r159 @ 'high_affinity_bin'
'THBS1_TGFB1(state~latent)_LTBP1_FN1_FBN1_dissociate' TGFB1(state{latent},w[1],VLAL[2],LSKL[3]), LTBP1(TB[1]), THBS1(WxxW[2],KRFK[3]) -> TGFB1(state{latent},w[1],VLAL[.],LSKL[.]), LTBP1(TB[1]), THBS1(WxxW[.],KRFK[.]) | 1 r160 @ 'low_affinity_un'
'THBS1_TGFB1(state~latent)_LTBP1_FN1_FBN1_Activate' LTBP1(TB[1]), THBS1(WxxW[2],KRFK[3]), TGFB1(state{latent},w[1],VLAL[2],LSKL[3]),. -> LTBP1(TB[.]), THBS1(WxxW[.],KRFK[.]), TGFB1(state{active},w[.],VLAL[.],LSKL[.]), iterateTGFB1() | 1 r161 @ 'high_affinity_un'


/* Binding of Decorin to Thrombospondin. FROM [1468447] Using a solid phase assay, intact DCN as well as its glycosaminoglycan-free core protein exhibits with KD values of about 5 nM and 2 nM, respectively, high affinity binding to thrombospondin. The addition of decorin or of its glycosaminoglycan-free core protein led to a considerable delay of cell attachment on a thrombospondin substrate. FROM [1550960], 125I-TGF-beta binds to purified TSP in an interaction that is specific in the sense that bound TGF-beta could be displaced by TGF-depleted TSP but not significantly by native TSP, heparin, decorin, alpha 2-macroglobulin, fibronectin, or albumin. Conclusion: DCN and TGFB1 have different binding sites on THBS1.*/
'THBS1_DCN' THBS1(x[.]), DCN(core[.]) -> THBS1(x[1]), DCN(core[1]) | 1 r162 @ 'high_affinity_bin'
'THBS1_DCN_dissociate' THBS1(x[1]), DCN(core[1]) -> THBS1(x[.]), DCN(core[.]) | 1 r163 @ 'very_high_affinity_un'

/* Binding of Thrombospondin to MMP2 and MMP9 [10900205,11606713] Using yeast two-hybrid system MMP2 has been shown to interact with the thrombospondins 1 (TSP1) and 2 (TSP2) properdin-like type 1 repeats (TSR).TSR interacted with the NH(2)-terminal region of the MMP2 that contains the catalytic domain. but Neither TSP1 nor TSP2 was degraded by MMP2 in vitro.Tissue culture and in vitro assays demonstrated that the presence of purified TSR and intact THBS1 resulted in inhibition of MMP activity. The ability of THBS1 to inhibit MMP3-dependent activation of pro-MMP9 and thrombin-induced activation of pro-MMP2 suggests that the TSPs may inhibit MMP activity by preventing activation of the MMP2 and MMP9*/
'MMP2_THBS1' MMP2(state{latent},Cat[.]), THBS1(z[.]) -> MMP2(state{latent},Cat[1]), THBS1(z[1]) | 1 r164 @ 'normal_affinity_bin'
'MMP2_THBS1_dissociate' MMP2(state{latent},Cat[1]), THBS1(z[1])-> MMP2(state{latent},Cat[.]), THBS1(z[.]) | 1 r165 @ 'high_affinity_un'
'MMP9_THBS1' MMP9(Cat[.]), THBS1(z[.]) -> MMP9(Cat[1]), THBS1(z[1]) | 1 r166 @ 'normal_affinity_bin'
'MMP9_THBS1_dissociate' MMP9(Cat[1]), THBS1(z[1]) -> MMP9(Cat[.]), THBS1(z[.]) | 1 r167 @ 'high_affinity_un'

/* ADAMTS1 cleaves Thrombospondin [17082774]*/
'ADAMTS1_THBS1' ADAMTS1(y[.]), THBS1(b[.]) -> ADAMTS1(y[1]), THBS1(b[1]) | 1 r168 @ 'normal_affinity_bin'
'ADAMTS1_THBS1_dissociate' ADAMTS1(y[1]), THBS1(b[1]) -> ADAMTS1(y[.]), THBS1(b[.]) | 1 r169 @ 'high_affinity_un'
'Degradation of THBS1 by ADAMTS1' ADAMTS1(y[1]), THBS1(b[1]) -> ADAMTS1(y[.]),. | 1 r170 @ 'high_affinity_un'

/* Binding of FBN1 to Thrombospondin. FROM [19755719], Using cell culture models, we defined primary and secondary elastic fiber interaction networks by identifying molecular interactions with the elastic fiber molecules fibrillin-1, MAGP1, fibulin-5, and lysyl oxidase.PF2 fragment from fibrillin-1 bound to endogenous fibrillin-1, IGFBP3, βigH3 (TGFBi), stanniocalcin-2, PAI-1, perlecan, thrombospondin-1, and LOXL.*/
'THBS1_FBN1' THBS1(c[.]), FBN1(PF2[.]) -> THBS1(c[1]), FBN1(PF2[1]) | 1 r171 @ 'low_affinity_bin'
'THBS1_FBN1_dissociate' THBS1(c[1]), FBN1(PF2[1]) -> THBS1(c[.]), FBN1(PF2[.]) | 1 r172 @ 'normal_affinity_un'

/* Binding of ITGAVB3 to Thrombospondin . FROM [14963009],TSP-1 has previously been shown to ligate the integrin heterodimers αIIbβ3, αvβ3 [2848850], α3β1 [10446179], α4β1, α5β1, and α6β4 [2475502]. NOTE: the role of this association in the context of activation of LAP-TGFB1 is not clear.*/
/*'THBS1_ITGAVB3' THBS1(RGD), ITGAVB3(x) -> THBS1(RGD!1), ITGAVB3(x!1) | 1 r173 @ 'normal_affinity_bin'*/

/* Binding and activation of MMP2. FROM model [21980344]; proMMP2 is activated on the cell surface through the formation of a ternary complex of active MT1-MMP and TIMP2 bound to proMMP2. Apperance of MMP14 at the cell surface is followed by binding of stochiometric concentration of TIMP2 to the catalytic site of MMP14. Pro-MMP2 then binds to the C-ter of TIMP2 and is cleaved and activated by a second MMP14. TIMP2 paradox: this inhibitor of both MT1-MMP and MMP2 activity is also required for pro-MMP2 activation leading to MMP2 formation at low concentration. FROM [8663332] Analysis of the rate of binding of TIMPs to the catalytic domain of MT1-MMP using kinetic methods showed that TIMP-1 is an extremely poor inhibitor of MT1-MMP. In comparison, TIMP-2 and TIMP-3 are excellent inhibitors, binding more rapidly to the catalytic domain of MT1-MMP than to the catalytic domain of gelatinase A. NDA: L INHIBITION D ACTIVITE DES MMP PAR LES TIMPS SE TRADUIT PAR UNE MISE EN COMPETITION DU SITE CATALYTIQUE ENTRE TIMP ET LES SUBSTRATS.*/
'MMP14(state~latent)_TIMP2' MMP14(state{latent},Cat[.]), TIMP2(Nter[.],Cter[.]) -> MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[.]) | 1 r174 @ 'k_bin'
'MMP14(state~latent)_TIMP2_dissociate' MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[.]) -> MMP14(state{latent},Cat[.]), TIMP2(Nter[.],Cter[.]) | 1 r175 @ 'k_un'
'MMP14_TIMP2_MMP2' MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[.]), MMP2(state{latent},PEX[.]) -> MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[2]), MMP2(state{latent},PEX[2]) | 1 r176 @ 'k_bin'
'MMP14_TIMP2_MMP2_dissociate' MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[2]), MMP2(state{latent},PEX[2]) -> MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[.]), MMP2(state{latent},PEX[.]) | 1 r177 @ 'kd_un'
'MMP14_TIMP2_MMP2-MMP14' MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[2]), MMP2(state{latent},PEX[2],Nter[.]), MMP14(state{active},Cat[.]) -> MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[2]), MMP2(state{latent},PEX[2],Nter[3]), MMP14(state{active},Cat[3]) | 1 r178 @ 'k_bin'
'MMP14_TIMP2_MMP2-MMP14_dissociate' MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[2]), MMP2(state{latent},PEX[2],Nter[3]), MMP14(state{active},Cat[3]) -> MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[2]), MMP2(state{latent},PEX[2],Nter[.]), MMP14(state{active},Cat[.]) | 1 r179 @ 'kd_un'
'Activation MMP2' MMP14(state{latent},Cat[1]), TIMP2(Nter[1],Cter[2]), MMP2(state{latent},PEX[2],Nter[3]), MMP14(state{active},Cat[3]) -> MMP14(state{latent},Cat[.]), TIMP2(Nter[.],Cter[.]), MMP2(state{active},PEX[.],Nter[.]), MMP14(state{active},Cat[.]) | 1 r180 @ 'k_un'

/* Inhibition of MMP2, MMP14 activities by high concentration, TIMP2. FROM [9368077] the CTD of MMP2 contains the high affinity binding site, whereas the catalytic domain contains the low affinity site.*/
'TIMP2_MMP2(state~active)' TIMP2(Nter[.]), MMP2(state{active},Cat[.]) -> TIMP2(Nter[1]), MMP2(state{active},Cat[1]) | 1 r181 @ 'l_bin'
'TIMP2_MMP2(state~active)_dissociate' TIMP2(Nter[1]), MMP2(state{active},Cat[1]) -> TIMP2(Nter[.]), MMP2(state{active},Cat[.]) | 1 r182 @ 'l_un'
'TIMP2_MMP14(state~active)' TIMP2(Nter[.]), MMP14(state{active},Cat[.]) -> TIMP2(Nter[1]), MMP14(state{active},Cat[1]) | 1 r183 @ 'l_bin'
'TIMP2_MMP14(state~active)_dissociate' TIMP2(Nter[1]), MMP14(state{active},Cat[1]) -> TIMP2(Nter[.]), MMP14(state{active},Cat[.]) | 1 r184 @ 'l_un'

/* Inhibition of MMP2, MMP9 and 14 activity by TIMP3 (From Review [20080133]).*/
'TIMP3_MMP14(state~active)' TIMP3(Nter[.]), MMP14(state{active},Cat[.]) -> TIMP3(Nter[1]), MMP14(state{active},Cat[1]) | 1 r185 @ 'normal_affinity_bin'
'TIMP3_MMP14(state~active)_dissociate' TIMP3(Nter[1]), MMP14(state{active},Cat[1]) -> TIMP3(Nter[.]), MMP14(state{active},Cat[.]) | 1 r186 @ 'high_affinity_un'
'TIMP3_MMP2(state~active)' TIMP3(Nter[.]), MMP2(state{active},Cat[.]) -> TIMP3(Nter[1]), MMP2(state{active},Cat[1]) | 1 r187 @ 'normal_affinity_bin'
'TIMP3_MMP2(state~active)_dissociate' TIMP3(Nter[1]), MMP2(state{active},Cat[1]) -> TIMP3(Nter[.]), MMP2(state{active},Cat[.]) | 1 r188 @ 'high_affinity_un'
'TIMP3_MMP9' TIMP3(Nter[.]), MMP9(Cat[.]) -> TIMP3(Nter[1]), MMP9(Cat[1]) | 1 r189 @ 'normal_affinity_bin'
'TIMP3_MMP9_dissociate' TIMP3(Nter[1]), MMP9(Cat[1]) -> TIMP3(Nter[.]), MMP9(Cat[.]) | 1 r190 @ 'high_affinity_un'

/* Inhibition of MMP2 and MMP9 activity by TIMP1*/
'TIMP1_MMP2' TIMP1(Nter[.]), MMP2(state{active},Cat[.]) -> TIMP1(Nter[1]), MMP2(state{active},Cat[1]) | 1 r191 @ 'low_affinity_bin'
'TIMP1_MMP2_dissociate' TIMP1(Nter[1]), MMP2(state{active},Cat[1]) -> TIMP1(Nter[.]), MMP2(state{active},Cat[.]) | 1 r192 @ 'low_affinity_un'
'TIMP1_MMP9' TIMP1(Nter[.]), MMP9(Cat[.]) -> TIMP1(Nter[1]), MMP9(Cat[1]) | 1 r193 @ 'high_affinity_bin'
'TIMP1_MMP9_dissociate' TIMP1(Nter[1]), MMP9(Cat[1]) -> TIMP1(Nter[.]), MMP9(Cat[.]) | 1 r194 @ 'very_high_affinity_un'

/* Inhibition of ADAMTS-4 by TIMP3. FROM [19643179], Our Ki values for TIMP3 inhibition of ADAMTS4 (Ki = 0.1–0.7 nM) are in agreement with that reported by Kashiwagi et al. (11278243), although higher values of 4–8 nM have been reported in other studies (11311239 and 17470431).*/
'ADAMTS4_TIMP3' ADAMTS4(Cat[.]), TIMP3(Nter[.]) -> ADAMTS4(Cat[1]), TIMP3(Nter[1]) | 1 r195 @ 'normal_affinity_bin'
'ADAMTS4_TIMP3_dissociate' ADAMTS4(Cat[1]), TIMP3(Nter[1])-> ADAMTS4(Cat[.]), TIMP3(Nter[.]) | 1 r196 @ 'high_affinity_un'

/* Degradation of type 1 Collagen by MMP14.FROM [8999957]MT1-MMP alias MMP14 is an extracellular matrix-degrading enzyme sharing the substrate specificity with interstitial collagenases.*/
'MMP14_COL1' MMP14(state{active},Cat[.]), COL1(d[.]) -> MMP14(state{active},Cat[1]), COL1(d[1]) | 1 r197 @ 'normal_affinity_bin'
'MMP14_COL1_dissociate' MMP14(state{active},Cat[1]), COL1(d[1]) -> MMP14(state{active},Cat[.]), COL1(d[.]) | 1 r198 @ 'high_affinity_un'
'Degradation of COL1 by MMP14' MMP14(state{active},Cat[1]), COL1(d[1]) -> MMP14(state{active},Cat[.]),. | 1 r199 @ 'high_affinity_un'

/* Binding-degradation of type I collagen by MMP9 and MMP2. FROM [23085623] MMP2 and MMP9 primarily utilize a unique collagen binding domain (CBD) consisting of three in-tandem fibronectin-like type II modules inserted into the catalytic domain for positioning collagen [1313021, 8120015,7744795] substrate molecules. Domain deletion of the CBD in MMP2 attenuated cleavage of gelatin (denatured collagen) by 90percent (8120015). Among those MMPs which most efficiently cleave the interstitial triple-helical collagens, MMP1, MMP8, MMP13, and MMP14, the hemopexin-like (PEX) C-terminal domain has been shown to contribute mostly as an exosite [1315762]. Whereas PEX is not critical for gelatin cleavage, additional studies point to contributions of both CBD and PEX during MMP2 cleavage of native type I collagen [11513874]. FROM [17298441 and 7890717]MMP9 and MP2, are able to cleave collagens I in its native form and in a manner that is characteristic of the unique collagenolytic activity of MMP collagenases. BUT BUT BUT FROM [11368514], purified MMP2 binds but does not cleave intact type I collagen.These data provide biochemical evidence that MMP2 exosite interactions with type I collagen may function in the posttranslational control of MMP2 activity by reducing the rate of autolytic inactivation.CONCLUSION, proMMP2 binds Type I collagen leading to stabilization (when MMP2 is active, it degrades substrates and itself) but does not degrade native type I collagen.NOTE: as the site CBD is within the catalytic site, we keep only Cat site (the others substrates are in competition for this site). Because the role of MMP2 on degradation of COL1 is controversed even if its binding is demonstrated and because the autoicatalysis process for MMP2 has never shown in vivo, the model did not include these processes.*/

'MMP9_COL1' MMP9(Cat[.]), COL1(c[.]) -> MMP9(Cat[1]), COL1(c[1]) | 1 r200 @ 'normal_affinity_bin'
'MMP9_COL1_dissociate' MMP9(Cat[1]), COL1(c[1]) -> MMP9(Cat[.]), COL1(c[.]) | 1 r201 @ 'high_affinity_un'
'Degradation of COL1 by MMP9' MMP9(Cat[1]), COL1(c[1]) -> MMP9(Cat[.]),. | 1 r202 @ 'high_affinity_un'

/* Binding-degradation of fibronectin by MMP9 and MMP2. FROM [9054449]In context of FN cleavage, it was demonstrated that the carboxyl-terminal hemopexin-like domain of MMP2 (PEX) is essential for binding of FN. FROM [20932906]The carboxyl-terminal hemopexin-like domain (PEX) of MMP2 was shown to possess critical exodomain properties required for the interactions of MMP2 with FN, and FN was cleaved at a significantly reduced rate by an MMP2 variant with deletion of PEX. FROM [23201304] MMP2 and MMP9 cleave fibronectin and contribute to cell migration by eliminating the surrounding ECM and basement membrane barriers. FROM [18039280]fibronectin avidly binds MMP2, MMP9 or MMP13 which are involved in fibronectin degradation of rat lung granulomatous fibrosis caused by Angiostrongylus cantonensis.FROM [18340378]The initial steps of ovarian cancer cell metastasis are mediated by MMP-2 cleavage of vitronectin and fibronectin. FROM [22159414], MT1-MMP regulates the turnover and endocytosis of extracellular matrix fibronectin. NDA ; MMP2 cleave FN1 via son site CAT mais requiert PEX libre.*/

'MMP9_FN1' MMP9(Cat[.]), FN1(e[.]) -> MMP9(Cat[1]), FN1(e[1]) | 1 r203 @ 'normal_affinity_bin'
'MMP9_FN1_dissociate' MMP9(Cat[1]), FN1(e[1]) -> MMP9(Cat[.]), FN1(e[.]) | 1 r204 @ 'high_affinity_un'
'MMP2_FN1' MMP2(state{active},Cat[.],PEX[.]), FN1(e[.]) -> MMP2(state{active},Cat[1],PEX[.]), FN1(e[1]) | 1 r205 @ 'normal_affinity_bin'
'MMP2_FN1_dissociate' MMP2(state{active},Cat[1],PEX[.]), FN1(e[1]) -> MMP2(state{active},Cat[.],PEX[.]), FN1(e[.]) | 1 r206 @ 'high_affinity_un'
'MMP14_FN1' MMP14(state{active},Cat[.]), FN1(e[.]) -> MMP14(state{active},Cat[1]), FN1(e[1]) | 1 r207 @ 'normal_affinity_bin'
'MMP14_FN1_dissociate' MMP14(state{active},Cat[1]), FN1(e[1]) -> MMP14(state{active},Cat[.]), FN1(e[.]) | 1 r208 @ 'high_affinity_un'
'Degradation of FN1 by MMP9' MMP9(Cat[1]), FN1(e[1]) -> MMP9(Cat[.]),. | 1 r209 @ 'very_high_affinity_un'
'Degradation of FN1 by MMP2' MMP2(state{active},Cat[1],PEX[.]), FN1(e[1]) -> MMP2(state{active},Cat[.],PEX[.]),. | 1 r210 @ 'very_high_affinity_un'
'Degradation of FN1 by MMP14' MMP14(state{active},Cat[1]), FN1(e[1]) -> MMP14(state{active},Cat[.]),. | 1 r211 @ 'very_high_affinity_un'
/* TGFBi binds to ITGAVB3. FROM [23792174] TGFBI, a transforming growth factor β-induced extracellular matrix protein, circulates at a level of ~300ng/ml in humans and modulates several integrin-mediated cellular functions. The protein contains an N-terminal EMI domain, four consecutive FAS1 domains, and the RGD motif. Each FAS1 domain and the RGD motif have been known to interact with avb3 integrin. */
/*Here, we found that the binding affinity (Kd) of TGFBI for αvβ3 integrin was approximately 3.8×10(-8)M, a value ~2300-fold higher than that of a single FAS1 domain. */
/*FROM [12270930]Identification of motifs in the fasciclin domains of the transforming growth factor-beta-induced matrix protein betaig-h3 that interact with the alphavbeta5 integrin.NDA:*/
/*Il n'existe pas de liens clairs entre TGFBi et l'activation du TGFbeta. Je préfère l'écarter car il rentre en compétition avec d'autres acteurs */
/*'TGFBi_ITGAVB3' TGFBi(RGD), ITGAVB3(x) -> TGFBi(RGD!1), ITGAVB3(x!1) @ 'high_affinity_bin'*/
/*TGFBi binds to ITGAVB5 by RGD_A VERIFIER*/
/*'TGFBi_ITGAVB5' TGFBi(RGD), ITGAVB5(x) -> TGFBi(RGD!1), ITGAVB5(x!1) @ 'normal_affinity_bin'*/

/* TGFBi binds to COL1, FN1, FBN1 FROM [17659994, review), TGFBI in both the native and recombinant form can bind to type I, II and IV collagens [9061001]. Similarly, Billings et al. [12034705] showed interactions of purified TGFBIp with fibronectin and type I collagen: collagen/gelatin domains in the N-terminal region of fibronectin mediated this interaction. Moreover, TGFBI mutations that commonly occur in certain corneal dystrophies do not apparently affect its binding to type I collagen, fibronectin and laminin or to forming fibrillar structures [11867580].*/
/*'TGFBi_COL1' TGFBi(x), COL1(c) -> TGFBi(x!1), COL1(c!1) @ 'normal_affinity_bin'*/
/*'TGFBi_FN1' TGFBi(y), FN1(z) -> TGFBi(y!1), FN1(z!1) @ 'normal_affinity_bin'*/
/*'TGFBi_FBN1' TGFBi(z), FBN1(y) -> TGFBi(z!1), FBN1(y!1) @ 'normal_affinity_bin'*/
